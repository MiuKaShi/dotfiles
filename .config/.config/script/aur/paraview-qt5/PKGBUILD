# Maintainer: A modify paraview-git

_pkgname=paraview
pkgname=$_pkgname-qt5
pkgrel=1
pkgver=v5.13.2.r1220.g2151063dea
pkgdesc="Open-source, multi-platform data analysis and visualization application"
arch=(x86_64)
url="https://www.paraview.org"
provides=(paraview)
conflicts=(paraview)
license=(BSD custom)
depends=(qt5-tools qt5-svg qt5-x11extras onetbb openmpi ffmpeg
	boost-libs protobuf python-mpi4py python-numpy
	cgns double-conversion expat freetype2 gdal gl2ps glew hdf5 libjpeg-turbo
	jsoncpp libjsoncpp.so libharu libxml2 lz4 xz netcdf
	libogg libpng pugixml libtiff zlib verdict)
optdepends=(python-matplotlib python-pandas)
makedepends=(cmake boost mesa gcc-fortran ninja)
source=("git+https://gitlab.kitware.com/paraview/${_pkgname}.git")
sha512sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	mkdir -p build
	cd "$_pkgname"
	git submodule update --init --recursive
}

build() {
	cd build
	cmake ../$_pkgname \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_CXX_STANDARD=17 \
		-DPARAVIEW_IGNORE_CMAKE_CXX11_CHECKS=ON \
		-DPARAVIEW_ENABLE_EMBEDDED_DOCUMENTATION=OFF \
		-DPARAVIEW_PLUGIN_DISABLE_XML_DOCUMENTATION=ON \
		-DPARAVIEW_USE_VTKM=OFF \
		-DPARAVIEW_ENABLE_XDMF3=ON \
		-DPARAVIEW_USE_PYTHON=ON \
		-DPARAVIEW_USE_MPI=ON \
		-DPARAVIEW_BUILD_WITH_EXTERNAL=ON \
		-DPARAVIEW_QT_VERSION=5 \
		-DVTK_QT_VERSION=5 \
		-DVTK_MODULE_USE_EXTERNAL_VTK_ioss=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_fmt=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_cli11=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_eigen=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_exprtk=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_fast_float=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_nlohmannjson=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_pegtl=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_token=OFF \
		-DVTK_MODULE_USE_EXTERNAL_VTK_utf8=OFF \
		-GNinja

	ninja ${MAKEFLAGS}
}

package() {
	cd build
	DESTDIR="${pkgdir}" ninja install
}
